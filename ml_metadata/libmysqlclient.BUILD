# Source files generated with cmake.
configure_out_srcs = [
    "build/libmariadb/ma_client_plugin.c",
]

# Header files generated with cmake.
configure_out_hdrs = [
    "build/include/ma_config.h",
    "build/include/config.h",
    "build/include/mariadb_version.h",
]

# A genrule to run cmake and generate configure_out_(srcs,hdrs).
genrule(
    name = "configure",
    srcs = glob(
        ["**"],
        exclude = ["bazel*/**"],
    ),
    outs = configure_out_srcs + configure_out_hdrs,
    cmd = "\n".join([
        "export INSTALL_DIR=$$(pwd)/$(@D)/build",
        "export TMP_DIR=$$(mktemp -d -t build.XXXXXX)",
        "mkdir -p $$TMP_DIR",
        "cp -R $$(pwd)/external/libmysqlclient/* $$TMP_DIR",
        "cd $$TMP_DIR",
        "mkdir build",
        "cd build",
        "cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_DISABLE_FIND_PACKAGE_Iconv=ON -DICONV_LIBRARIES=$${CONDA_PREFIX}/lib/libiconv.dylib -DICONV_INCLUDE_DIR=$${CONDA_PREFIX}/include $${CMAKE_ICONV_FLAG-}",
        "cd ..",
        "cp -R ./build/* $$INSTALL_DIR",
        "rm -rf $$TMP_DIR",
    ]),
)

config_setting(
    name = "darwin",
    values = {"cpu": "darwin"},
    visibility = ["//visibility:public"],
)

config_setting(
    name = "darwin_arm64",
    values = {"cpu": "darwin_arm64"},
    visibility = ["//visibility:public"],
)

cc_library(
    name = "libmysqlclient",
    srcs = configure_out_srcs + [
        # plugins.
        "plugins/auth/my_auth.c",
        "plugins/auth/old_password.c",
        "plugins/pvio/pvio_socket.c",
        # ssl.
        "libmariadb/secure/openssl.c",
        # core libmariadb
        "libmariadb/ma_array.c",
        "libmariadb/ma_charset.c",
        "libmariadb/ma_hash.c",
        "libmariadb/ma_net.c",
        "libmariadb/mariadb_charset.c",
        "libmariadb/ma_time.c",
        "libmariadb/ma_default.c",
        "libmariadb/ma_errmsg.c",
        "libmariadb/mariadb_lib.c",
        "libmariadb/ma_list.c",
        "libmariadb/ma_pvio.c",
        "libmariadb/ma_tls.c",
        "libmariadb/ma_alloc.c",
        "libmariadb/ma_compress.c",
        "libmariadb/ma_init.c",
        "libmariadb/ma_password.c",
        "libmariadb/ma_ll2str.c",
        "libmariadb/ma_sha1.c",
        "libmariadb/mariadb_stmt.c",
        "libmariadb/ma_loaddata.c",
        "libmariadb/ma_stmt_codec.c",
        "libmariadb/ma_string.c",
        "libmariadb/ma_dtoa.c",
        "libmariadb/mariadb_dyncol.c",
        "libmariadb/mariadb_async.c",
        "libmariadb/ma_context.c",
        "libmariadb/ma_io.c",
    ],
    hdrs = configure_out_hdrs + glob([
        "include/*.h",
        "include/mysql/*.h",
        "include/mariadb/*.h",
    ]),
    copts = [
        "-g",
        "-DLIBMARIADB",
        "-DTHREAD",
        "-DHAVE_COMPRESS",
        "-DENABLED_LOCAL_INFILE",
        "-DLIBICONV_PLUG",
        "-DHAVE_OPENSSL",
        "-DHAVE_TLS",
        # Fix implicit function declarations
        "-D_GNU_SOURCE",
    ] + select({
        # On Linux/GCC 13 / Ubuntu 24.04: define legacy MySQL types using stdint.h types
        # On macOS: these types are already correct size, don't redefine to avoid conflicts
        ":darwin": [],
        ":darwin_arm64": [],
        "//conditions:default": [
            "-Duint=uint32_t",
            "-Dushort=uint16_t",
            "-Dulong=uint64_t",
        ],
    }),
    includes = [
        "build/include/",
        "include/",
        "include/mariadb/",
    ],
    linkopts = [
        "-lpthread",
        "-ldl",
        "-lm",
    ] + select({
        ":darwin": ["-liconv"],
        ":darwin_arm64": ["-liconv"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        "@boringssl//:ssl",
        "@zlib",
    ],
)
